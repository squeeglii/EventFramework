plugins {
    id("java")
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.papermc.paperweight.userdev") version "1.7.1"
}

boolean isModVersionDefined = project.properties["plugin_version"] == null ||
        project.properties["plugin_version"].toString().length() == 0

String pluginVer = isModVersionDefined
        ? "indev"
        : project.properties["plugin_version"].toString()

group = project.properties.maven_group
version = "%s+%s.spigot".formatted(pluginVer, project.properties["minecraft_version"])

repositories {
    mavenCentral()
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "mojang"
        url = 'https://libraries.minecraft.net/'
    }
    maven {
        name = "command-api"
        url = "https://repo.codemc.org/repository/maven-public/"
    }
    maven {
        name = "Paper MC"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

paperweight.reobfArtifactConfiguration = io.papermc.paperweight.userdev.ReobfArtifactConfiguration.Companion.MOJANG_PRODUCTION

dependencies {
    paperweight.paperDevBundle(project.properties["spigot_distribution"])

    implementation("dev.jorel:commandapi-bukkit-shade-mojang-mapped:${project.properties["command_api_version"]}")
}

shadowJar {
    dependencies {
        exclude(dependency("com.mojang:brigadier"))
    }

    // Shade in command api and avoid conflicts :)
    relocate("dev.jorel.commandapi", "me.squeeglii.plugin.eventfw.commandapi")
}

processResources {
    inputs.property "version", project.version
    filteringCharset 'UTF-8'

    filesMatching('plugin.yml') {
        expand project.properties
    }

    filesMatching('paper-plugin.yml') {
        expand project.properties
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion.set(JavaLanguageVersion.of(targetJavaVersion))
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}


static def formatAsBadgeSafe(var val) {
    if(!(val instanceof String))
        return val

    // Badges break if hyphens are present.
    return ((String) val).replaceAll("[- /]", "_")
}

tasks.register('update-doc-templates', Copy) {
    group = "squee-gradle"
    description = "Takes documents from a folder, expands any template variables, and then copies them to the root."
    doNotTrackState("Stops gradle from screaming - It doesn't let files get copied to the root if tracking state.")

    Map filteredSrc = project.properties.collectEntries {
        k, v -> [(k): formatAsBadgeSafe(v) ]
    }

    from "/template_docs"

    into "/"
    include "**"
    exclude "media/**"

    expand filteredSrc
}
